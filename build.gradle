plugins {
    id 'org.springframework.boot' version "2.3.4.RELEASE" apply false
    id "io.spring.dependency-management" version "1.0.10.RELEASE" apply false
    id "com.github.ManifestClasspath" version "0.1.0-RELEASE" apply false
    id "org.unbroken-dome.test-sets" version "2.1.1" apply false
}

subprojects { project ->
    repositories {
        mavenLocal()
        mavenCentral()
    }
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'idea'
    if (project.name.startsWith("service-") || project.name.startsWith("itests-")) {
        apply plugin: 'io.spring.dependency-management'
        apply plugin: "org.springframework.boot"
        apply plugin: 'org.unbroken-dome.test-sets'
        apply plugin: "com.github.ManifestClasspath"
        apply from: '../gradle-tasks/docker_compose.gradle'
        compileJava.dependsOn(processResources)
        dependencyManagement {
            imports {
                mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            }
        }
        apply from: "../gradle-common/service.gradle"
        apply from: "../gradle-common/itest.gradle"
    }
    apply from: "../gradle-common/test.gradle"
    apply from: "../gradle-common/lombok.gradle"
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Test) {
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = 'full'
    }
}